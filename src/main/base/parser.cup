package com.codigo;

import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;


parser code {:
    public Lexer lexer;
    private boolean sintaxisCorrecta = true;

    public Parser(Lexer lexer) {
        super(lexer);
        this.lexer = lexer;
    }

    public void syntax_error(Symbol s) {
        sintaxisCorrecta = false;
        String errorMsg = "Error de sintaxis en línea " + (s.left + 1) +
                          ", columna " + (s.right + 1) +
                          ". Token inesperado: '" + s.value + "'";
        System.err.println(errorMsg);
    }

    public void report_error(String message, Object info) {
        if (info instanceof Symbol) {
            Symbol s = (Symbol) info;
            if (s.sym == sym.EOF) {
                System.err.println("Error inesperado al final del archivo");
            } else {
                System.err.println("Error: " + message);
            }
        }
    }

    public boolean parseCode() {
        try {
            super.parse();
            if (sintaxisCorrecta) {
                System.out.println("Compilación exitosa");
                return true;
            } else {
                System.out.println("Se encontró un error de sintaxis.");
                return false;
            }
        } catch (Exception e) {
            System.out.println("Error fatal: " + e.getMessage());
            return false;
        }
    }
:};


action code {:
:}

/* Terminales */
terminal AGRUPACIONPRINCIPAL, DECLARACIONFUNCION
        , AGRUPACIONESTRUCTURA, AGRUPACIONENUMERATOR
        , AGRUPACIONINTERFAZ, AGRUPACIONSECUNDARIA;
terminal LISTA;
terminal POR, MIENTRAS, SELECTOR, CASO, DEFECTO, PORCADA, INTENTAR, HACER
        , ATRAPAR, TERMINAR, IMPRIMIR, ESCANEAR, SI, SINO, SINOHACER;
terminal RETORNAR, PARAR;
terminal TIPONUMERICO, TIPOLOGICO, TIPOCADENA, TIPOINFERIDO, TIPOCARACTER;
terminal OPERADORRELACIONAL, OPERADORASIGNAMIENTO
        , OPERADORRESULTADO, OPERADORDESPLAZAMIENTO, OPERADORTERNARIO
        , OPERADORCONLOGICO;
terminal PARAMETROENTRADA, GESTIONRECURSOS, MODIFICADORACCESO, DEC;
terminal CORCHETEABIERTO, CORCHETECERRADO, LLAVEABIERTA, LLAVECERRADA
        , PARENTESISABIERTO, PARENTESISCERRADO, NUMERICO, COMA, PUNTOCOMA
        , PUNTODOBLE, LITERAL, ID;
terminal MENOS, MAS, MULTI, DIV;

/* Inicio */
non terminal inicio, inicios;

/* instrucciones */

non terminal espacio,
            librerias,
            clases,
            funciones,
            estructuras,
            enums,
            interfaces,
            declaraciones,
            sis,
            mientras,
            mientrashacer,
            porpuro,
            porcadas,
            intentos,
            impresiones,
            escaneos,
            declaracionarreglos,
            declaracionlistas,
            lambdas,
            ternarios,
            selectors;

/* reglas */
non terminal casos, defectos, atrapars, terminars, retornos, asignars
            , oraciones, asignamientovariables, valores
            , tipos, listabloques, dimensiones, modificadoresacceso
            , miembrosclase, bloquevalores, libreriasopcional
            , declaracionesopcional, clasesopcional, dimensionesopcional, bloquedeclaraciones
            , sinoshacer, bloqueexpresiones, bloqueparametros, bloquecondiciones;

non terminal condiciones
            , parametros, operaritmetics, expresiones;

non terminal caso, declaracion
            , parametro, expresion, operaritmetic, atraparterminar, atrapar
            , condicion, sinohacer, sinos, selector, declaracionlista
            , alto, retornoalto, cuerpolambda, parametrolista, tipofuncionopcional
            , tipofuncion, valor, si, aumentodecremento, sino, retornoopcional
            , idnum, libreria, clase, bloqueoperaritmetics, miembroclase
            , declarador, cuerpolista, lambda, listadeclarar;

/* Precedencias*/
precedence right OPERADORTERNARIO, PUNTODOBLE;
precedence left MAS, MENOS;
precedence left MULTI, DIV;
precedence left LLAVEABIERTA;
precedence left PARENTESISCERRADO;
precedence left SINO, SINOHACER;
precedence left MODIFICADORACCESO, DECLARACIONFUNCION;
precedence left RETORNAR;
precedence left NUMERICO, COMA;
precedence left PARAR;

/* gramatica */
start with inicios;

inicios ::= inicio inicios | inicio;
inicio ::= espacio;

bloqueexpresiones ::= LLAVEABIERTA expresiones LLAVECERRADA;
expresiones ::= expresion expresiones | expresion;
expresion ::= sis | mientras | mientrashacer | porpuro | porcadas | intentos
                | impresiones | escaneos | declaracion | declaracionarreglos
                | declaracionlistas | lambdas | ternarios | asignars 
                | selectors | enums;

bloquecondiciones ::= PARENTESISABIERTO condiciones PARENTESISCERRADO;
condiciones ::= condicion OPERADORCONLOGICO condiciones | condicion;
condicion ::= valor OPERADORRELACIONAL valor;

bloqueparametros ::= PARENTESISABIERTO parametros PARENTESISCERRADO;
parametros ::= parametrolista | /* empty */;
parametrolista ::= parametro | parametrolista COMA parametro;
parametro ::= tipos ID;

bloquevalores ::= CORCHETEABIERTO listabloques CORCHETECERRADO
                | LLAVEABIERTA valores LLAVECERRADA;
valores ::= valor | valores COMA valor;
valor ::= LITERAL | NUMERICO | ID | bloqueoperaritmetics;

/* namespace */

espacio ::= AGRUPACIONPRINCIPAL ID 
            LLAVEABIERTA libreriasopcional declaracionesopcional 
            clasesopcional LLAVECERRADA;
clasesopcional ::= clases | /* empty */;
libreriasopcional ::= librerias | /* empty */;
declaracionesopcional ::= declaraciones | /* empty */;

/* librerias */

librerias ::= librerias libreria | libreria;
libreria ::= GESTIONRECURSOS ID PUNTOCOMA;

/* clases */

clases ::= clases clase | clase;
clase ::= modificadoresacceso AGRUPACIONSECUNDARIA ID
        LLAVEABIERTA miembrosclase LLAVECERRADA;
modificadoresacceso ::= MODIFICADORACCESO | /* empty */;
miembrosclase ::= miembroclase miembrosclase | /* empty */;
miembroclase ::= declaracion | enums | funciones | estructuras | interfaces;

/* funcion */

funciones ::= modificadoresacceso tipofuncionopcional DECLARACIONFUNCION ID 
        bloqueparametros LLAVEABIERTA expresiones 
        retornoopcional LLAVECERRADA;
retornoopcional ::= retornos | /* empty */;
tipofuncionopcional ::= tipofuncion | /* empty */;
tipofuncion ::= TIPONUMERICO | TIPOLOGICO | TIPOCADENA;

retornos ::= RETORNAR valor PUNTOCOMA;

/* estructura */

estructuras ::= AGRUPACIONESTRUCTURA ID bloquedeclaraciones;

/* enum */ 

enums ::= AGRUPACIONENUMERATOR ID OPERADORASIGNAMIENTO bloquevalores PUNTOCOMA;

/* interfaz */

interfaces ::= AGRUPACIONINTERFAZ ID bloquedeclaraciones;

/* declaracion */ 

bloquedeclaraciones ::= LLAVEABIERTA declaraciones LLAVECERRADA;
declaraciones ::= declaracion | declaraciones declaracion;
declaracion ::= DEC tipos listadeclarar PUNTOCOMA;
listadeclarar ::= declarador | listadeclarar COMA declarador;
declarador ::= ID | ID OPERADORASIGNAMIENTO valor;

asignars ::= ID OPERADORASIGNAMIENTO valor PUNTOCOMA;

/* Selector */

selectors ::= selector LLAVEABIERTA casos defectos LLAVECERRADA 
                | selector LLAVEABIERTA defectos LLAVECERRADA
                | selector LLAVEABIERTA casos LLAVECERRADA;
selector ::= SELECTOR PARENTESISABIERTO valor PARENTESISCERRADO;

casos ::= caso casos | caso;
caso ::= CASO valor PUNTODOBLE expresiones retornoalto;
defectos ::= DEFECTO PUNTODOBLE expresiones retornoalto;

retornoalto ::= retornos | alto;
alto ::= PARAR PUNTOCOMA;

/* if */

sis ::= si sinoshacer sinos;

sinos ::= /* empty */ | sino;
sinoshacer ::= /* empty */ | sinohacer sinoshacer;

si ::= SI bloquecondiciones bloqueexpresiones;

sinohacer::= SINOHACER bloquecondiciones bloqueexpresiones;
sino ::= SINO bloqueexpresiones;

/* while */

mientras ::= MIENTRAS bloquecondiciones PUNTODOBLE bloqueexpresiones;

/* do while */

mientrashacer ::= HACER bloquecondiciones PUNTODOBLE bloqueexpresiones;

/* for */

porpuro ::= POR PARENTESISABIERTO declaracion 
aumentodecremento condiciones PARENTESISCERRADO bloqueexpresiones;
aumentodecremento ::= ID OPERADORDESPLAZAMIENTO PUNTOCOMA;

/* foreach */

porcadas ::= PORCADA 
            PARENTESISABIERTO tipos ID PARAMETROENTRADA ID PARENTESISCERRADO
            PUNTODOBLE bloqueexpresiones;
tipos ::= TIPOCADENA | TIPOINFERIDO | TIPONUMERICO | TIPOCARACTER;

/* try */

intentos ::= INTENTAR bloqueexpresiones atraparterminar;
atraparterminar::= atrapars terminars | atrapars | terminars;
atrapars ::= atrapar atrapars | atrapar;
atrapar ::= ATRAPAR bloqueexpresiones;
terminars ::= TERMINAR bloqueexpresiones;

/* imprimir */

impresiones ::= IMPRIMIR 
PARENTESISABIERTO oraciones PARENTESISCERRADO PUNTOCOMA;
oraciones ::= valor | oraciones MAS valor;

/* escanear */

escaneos ::= ESCANEAR PARENTESISABIERTO PARENTESISCERRADO PUNTOCOMA;

/* arreglo */

declaracionarreglos ::=tipos CORCHETEABIERTO dimensionesopcional CORCHETECERRADO 
                    ID asignamientovariables PUNTOCOMA;
asignamientovariables ::= OPERADORASIGNAMIENTO listabloques

                    | /* empty */;

listabloques ::= bloquevalores
                | listabloques COMA bloquevalores;
dimensionesopcional::= dimensiones | /* empty */;
dimensiones ::= NUMERICO | dimensiones COMA NUMERICO;

/* lista */

declaracionlistas ::= cuerpolista PUNTOCOMA | declaracionlista;
declaracionlista ::= cuerpolista OPERADORASIGNAMIENTO bloquevalores PUNTOCOMA;
cuerpolista ::= LISTA CORCHETEABIERTO tipos CORCHETECERRADO ID;

/* lambda */

lambdas ::= ID OPERADORASIGNAMIENTO lambda PUNTOCOMA | lambda PUNTOCOMA;
lambda ::= bloqueparametros OPERADORRESULTADO cuerpolambda;
cuerpolambda ::= bloqueexpresiones 
                | bloqueoperaritmetics;

/* operacion ternaria */

ternarios ::= ID OPERADORASIGNAMIENTO bloquecondiciones OPERADORTERNARIO
                valor PUNTODOBLE valor PUNTOCOMA;

/* operacion aritmetica*/

bloqueoperaritmetics ::= PARENTESISABIERTO operaritmetics PARENTESISCERRADO;
operaritmetics ::= operaritmetics MAS operaritmetic
                | operaritmetics MENOS operaritmetic
                | operaritmetic;
operaritmetic ::= operaritmetic MULTI idnum | operaritmetic DIV idnum | idnum;
idnum ::= ID | NUMERICO;
